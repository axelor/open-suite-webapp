buildscript {
  repositories {
    gradlePluginPortal()
  }
  dependencies {
    classpath "com.diffplug.spotless:spotless-plugin-gradle:8.0.0"
  }
}

def getHeaderFile = { Project proj, String style ->
  def fileName = style == null ? "header.txt" : "header-${style}.txt"
  def paths = [
          "src/license", ".", proj.getRootDir().getPath() + "/gradle/license", proj.getRootDir().getPath()
  ]
  for (def path : paths) {
    final File file = proj.file(path + "/" + fileName);
    if (file.exists()) {
      return file;
    }
  }
  return null
}


File spotlessXmlFile = file("${projectDir}/gradle/spotless.xml.prefs")
allprojects { p ->

  // 'modules' directory is shown as a project : need to be excluded
  if (!p.buildFile.exists()) {
    return;
  }

  apply plugin: com.diffplug.gradle.spotless.SpotlessPlugin

  spotless {
    enforceCheck false

    java {
      target fileTree(projectDir) {
        include 'src/*/java/**/*.java'
        exclude '**/build/**'
        exclude '**/bin/**'
        exclude '**/out/**'
      }
      googleJavaFormat()
      licenseHeaderFile(getHeaderFile(project, null))
    }
    format 'misc', {
      target fileTree(projectDir) {
        include 'src/**/*.xml'
        exclude '**/build/**'
        exclude '**/bin/**'
        exclude '**/.project/**'
        exclude '**/.classpath/**'
        exclude '**/.settings/**'
        exclude '**/.metadata/**'
        exclude '**/out/**'
        exclude '**/.idea/**'
      }
      eclipseWtp('xml').configFile(spotlessXmlFile)
      trimTrailingWhitespace()
    }
  }

  if (p.file('src/main/resources/domains').exists()) {
    p.afterEvaluate {
      p.spotlessJava.dependsOn(p.generateCode)
    }
  }

  tasks.register('formatCode') {
    description "Add license and format code."
    group "Axelor"
    dependsOn 'spotlessApply'
  }
  build.dependsOn 'spotlessApply'
}
